// Declare git functions
ext.gitSha = {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        def errorMessage = p.errorStream.text
        if (errorMessage.contains('Not a git repository')) {
            return "UNVERSIONED"
        } else {
            throw new RuntimeException(errorMessage)
        }
    }

    return p.text.trim()
}

ext.gitTimestamp = {
    def p = 'git log -n 1 --format=%at'.execute([], rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        def errorMessage = p.errorStream.text
        if (errorMessage.contains('Not a git repository')) {
            return 0
        } else {
            throw new RuntimeException(errorMessage)
        }
    }

    return p.text.trim()
}

// Disable Pre-dex since CI only does clean builds
project.android.dexOptions.preDexLibraries = false

// Add Git metadata to the BuildConfig class
project.android.defaultConfig {
    buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""
    buildConfigField 'long', 'GIT_TIMESTAMP', "${gitTimestamp()}L"
}

// Add metadata to the artifact's pom.xml
uploadArchives.repositories.mavenDeployer.pom.project {
    url = "https://github.com/genexuslabs/AndroidFlexibleClient"
    properties {
        vcs_hash = ext.gitSha()
    }
}
